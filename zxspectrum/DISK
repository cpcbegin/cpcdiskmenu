  10 REM Menu disquete (CPCBEGIN - 2021).
  20 PAPER 1: BORDER 1
  30 CLS
  40 DIM b$(20,12)
  50 LET h$="MY OWN COMPILATION 1"
  60 LET X=14-LEN (h$)/2
  70 LET E=4+LEN (h$)
  80 PAPER 2
  90 FOR Y=1 TO 3
 100 PRINT AT Y,X;: FOR I=1 TO E: PRINT " ";: NEXT I
 110 NEXT Y
 120 REM PAPER 2
 130 PRINT AT 2,X+2; INK 6;h$;
 140 PAPER 0: READ Z
 150 REM LET a$=""
 160 FOR I=1 TO Z
 170 READ a$,b$(I)
 180 PRINT AT I*(2+(z>10))+5,5;
 190 PAPER 1
 200 PRINT INK 5;" ";i;") ";
 210 PRINT INK 7;a$;
 220 NEXT i
 230 PRINT INK 3;: PRINT AT 21,3;: PRINT "(c) 2021, MENSAJE COPYRIGHT";
 240 LET o=1
 250 PRINT AT o*(2+(z>10))+5,5;: PAPER 5: PRINT INK 2;: PRINT " ";o;") ";
 260 LET a$=INKEY$
 270 IF a$="" THEN GO TO 260
 280 IF a$=CHR$ 241 OR a$=CHR$ 10 THEN PAPER 1: PRINT AT o*(2+(z>10))+5,5; INK 5;" ";o;") ";: LET o=o+1: IF o=Z+1 THEN LET o=Z
 290 IF a$=CHR$ 240 OR a$=CHR$ 11 THEN PAPER 1: PRINT AT o*(2+(z>10))+5,5; INK 5;" ";o;") ";: LET o=o-1: IF o=0 THEN LET o=1
 300 IF VAL ("0"+a$)>0 THEN LET o=VAL a$: GO TO 320
 310 IF  CODE a$<>13 AND a$<>"X" AND a$<>"Z" AND a$<>" " THEN LET a$="": GO TO 250
 320 GO SUB 370
 330 CLS : PRINT INK 1;"LOADING... ";b$(o);
 340 LOAD b$(o)
 350 CLS
 360 STOP
 370 FOR j=1 TO z
 380 PAPER 1: PRINT INK 5;
 390 IF o=j THEN PAPER 5: PRINT INK 1;
 400 PRINT AT j*(2+(z>10))+5,5;: PRINT " ";j;: PRINT ") ";
 410 NEXT j
 420 RETURN
 430 REM Use this syntax in DATA line:
 440 REM DATA number of programs,description 1,access 1,description 2,access 2,...
 450 DATA 5,"MY TOOL","MYTOOL.BAS","MY GAME","MYGAME.BAS","MY DEMO","MYDEMO.BAS","MY APP","MYAPP.BAS","OTHER STUFF","STUFF.BAS"
 460 REM CHANGE LINES 50 AND 320 WITH YOUR OWN DATA
 470 REM YOU CAN USE CURSORS, MAIN JOYSTICK OR TYPE THE NUMBER OF OPTIONS
 480 REM SELECT WITH SPACE OR ANY FIRE
